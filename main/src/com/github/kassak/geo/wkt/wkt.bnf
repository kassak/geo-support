{
  parserClass = "com.github.kassak.geo.wkt.WktParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Wkt"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.kassak.geo.wkt.psi"
  psiImplPackage="com.github.kassak.geo.wkt.psi.impl"

  elementTypeHolderClass="com.github.kassak.geo.wkt.WktTypes"
  elementTypeClass="com.github.kassak.geo.wkt.WktElementType"
  tokenTypeClass="com.github.kassak.geo.wkt.WktTokenType"
  parserUtilClass="com.github.kassak.geo.wkt.WktGeneratedParserUtil"

  pin(".*_tagged_text") = 1
  pin(".*_recover") = 0
  consumeTokenMethod(".*_recover") = "fast"

  tokens = [
    LPAREN = "("
    RPAREN = ")"
    LBRACKET = "["
    RBRACKET = "]"
    LBRACE = "{"
    RBRACE = "|"
    COMMA = ","
    DOT = "."
    SEMICOLON = ";"
    MINUS = "-"
    PLUS = "+"
    EQUAL = "="
    NUMBER = "NUMBER"
    SCIENTIFIC = "SCIENTIFIC"
    IDENT = "IDENT"
    STRING = "STRING"
  ]
}

root ::= <<parsePrimitives (emb_srid? any_geometry_tagged_text)>>

integer ::= NUMBER {name="integer"}
numeric ::= ['+' | '-'] <<parseFloatTerm>> {name="float"}
private unknown_identifier ::= <<parseUnknownIdentifier>>
private attr_list_recover ::= !(')' | ']')
private comma_attr_list_item_recover ::= attr_list_recover !','
meta private attrs_content ::= <<attrs>> {recoverWhile=attr_list_recover}
meta private attrs_inner ::= <<left>> <<attrs_content <<attrs>>>> <<right>> {pin=1}
meta attributes ::=
    <<attrs_inner '(' <<attrs>> ')'>>
  | <<attrs_inner '[' <<attrs>> ']'>>
meta private comma_list ::= <<item>> (',' <<item>>)* {pin(".*")=1}
meta private comma_list_attrs_item ::= <<item>> {recoverWhile=comma_attr_list_item_recover}
meta private comma_list_attrs ::= <<attributes <<comma_list <<comma_list_attrs_item <<item>>>>>>>>

private numeric_2 ::= numeric numeric
private numeric_3 ::= numeric numeric numeric
private numeric_4 ::= numeric numeric numeric numeric

emb_srid ::= SRID '=' NUMBER ';' {pin=1}

private any_geometry_tagged_text ::=
    geometry_tagged_text
  | wildcard_geometry_tagged_text

private meta with_dim ::=
    ZM <<withDim '"ZM"' <<proceed>>>>
  | Z  <<withDim '"Z"'  <<proceed>>>>
  | M  <<withDim '"M"'  <<proceed>>>>
  |    <<withDim '""'   <<proceed>>>>

private dim_coord ::=
    &<<dimIs "ZM">> numeric_4
  | &<<dimIs "Z">>  numeric_3
  | &<<dimIs "M">>  numeric_3
  | &<<dimIs "">>   numeric_2

private geometry_tagged_text ::=
    point_tagged_text
  | linestring_tagged_text
  | polygon_tagged_text
  | polyhedralsurface_tagged_text
  | triangle_tagged_text
  | tin_tagged_text
  | multipoint_tagged_text
  | multilinestring_tagged_text
  | multipolygon_tagged_text
  | geometrycollection_tagged_text

empty_set ::= EMPTY
point_tagged_text ::= POINT <<with_dim point_text>>
linestring_tagged_text ::= LINESTRING <<with_dim linestring_text>>
polygon_tagged_text ::= POLYGON <<with_dim polygon_text>>
polyhedralsurface_tagged_text ::= POLYHEDRALSURFACE <<with_dim polyhedralsurface_text>>
triangle_tagged_text ::= TRIANGLE <<with_dim polygon_text>>
tin_tagged_text ::= TIN <<with_dim polyhedralsurface_text>>
multipoint_tagged_text ::= MULTIPOINT <<with_dim multipoint_text>>
multilinestring_tagged_text ::= MULTILINESTRING <<with_dim multilinestring_text>>
multipolygon_tagged_text ::= MULTIPOLYGON <<with_dim multipolygon_text>>
geometrycollection_tagged_text ::= GEOMETRYCOLLECTION <<with_dim geometrycollection_text>>

private point_text ::= empty_set
  | <<attributes dim_coord>>
private linestring_text ::= empty_set
  | <<comma_list_attrs dim_coord>>
private polygon_text ::= empty_set
  | <<comma_list_attrs linestring_text>>
private polyhedralsurface_text ::= empty_set
  | <<comma_list_attrs polygon_text>>
private multipoint_text ::= empty_set
  | <<comma_list_attrs point_text>>
private multilinestring_text ::= polygon_text
private multipolygon_text ::= polyhedralsurface_text
private geometrycollection_text ::= empty_set
  | <<comma_list_attrs geometry_tagged_text>>

wildcard_geometry_tagged_text ::=
  unknown_identifier wildcard_geometry_text {pin=0}
private wildcard_geometry_text ::= <<comma_list_attrs (wildcard_geometry_attr*)>>
private wildcard_geometry_attr ::= numeric | STRING | any_geometry_tagged_text | wildcard_geometry_text
