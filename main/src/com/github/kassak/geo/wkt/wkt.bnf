{
  parserClass = "com.github.kassak.geo.wkt.WktParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Wkt"
  psiImplClassSuffix="Impl"
  psiPackage="com.github.kassak.geo.wkt.psi"
  psiImplPackage="com.github.kassak.geo.wkt.psi.impl"

  elementTypeHolderClass="com.github.kassak.geo.wkt.WktTypes"
  elementTypeClass="com.github.kassak.geo.wkt.WktElementType"
  tokenTypeClass="com.github.kassak.geo.wkt.WktTokenType"
  parserUtilClass="com.github.kassak.geo.wkt.WktGeneratedParserUtil"

  extends("wkt_.*_geom") = wkt_geometry
  pin("wkt_.*_geom") = 1
  pin(".*_recover") = 0
  consumeTokenMethod(".*_recover") = "fast"

  tokens = [
    LPAREN = "("
    RPAREN = ")"
    LBRACKET = "["
    RBRACKET = "]"
    LBRACE = "{"
    RBRACE = "|"
    COMMA = ","
    DOT = "."
    SEMICOLON = ";"
    MINUS = "-"
    PLUS = "+"
    EQUAL = "="
    NUMBER = "NUMBER"
    SCIENTIFIC = "SCIENTIFIC"
    IDENT = "IDENT"
    STRING = "STRING"
  ]
}

root ::= <<parsePrimitives (emb_srid? wkt_geometry)>>

integer ::= NUMBER {name="integer"}
numeric ::= ['+' | '-'] <<parseFloatTerm>> {name="float"}
private unknown_identifier ::= <<parseUnknownIdentifier>>
private attr_list_recover ::= !(')' | ']')
private comma_attr_list_item_recover ::= attr_list_recover !','
meta private attrs_content ::= <<attrs>> {recoverWhile=attr_list_recover}
meta private attrs_inner ::= <<left>> <<attrs_content <<attrs>>>> <<right>> {pin=1}
meta attributes ::=
    <<attrs_inner '(' <<attrs>> ')'>>
  | <<attrs_inner '[' <<attrs>> ']'>>
meta private comma_list ::= <<item>> (',' <<item>>)* {pin(".*")=1}
meta private comma_list_attrs_item ::= <<item>> {recoverWhile=comma_attr_list_item_recover}
meta private comma_list_attrs ::= <<attributes <<comma_list <<comma_list_attrs_item <<item>>>>>>>>

private numeric_2 ::= numeric numeric
private numeric_3 ::= numeric numeric numeric
private numeric_4 ::= numeric numeric numeric numeric

emb_srid ::= SRID '=' NUMBER ';' {pin=1}

wkt_geometry ::=
    wkt_tagged_geom
  | wkt_wildcard_geom

private meta with_dim ::=
    ZM <<withDim '"ZM"' <<proceed>>>>
  | Z  <<withDim '"Z"'  <<proceed>>>>
  | M  <<withDim '"M"'  <<proceed>>>>
  |    <<withDim '""'   <<proceed>>>>

private dim_coord ::=
    &<<dimIs "ZM">> numeric_4
  | &<<dimIs "Z">>  numeric_3
  | &<<dimIs "M">>  numeric_3
  | &<<dimIs "">>   numeric_2

private wkt_tagged_geom ::=
    wkt_point_geom
  | wkt_line_string_geom
  | wkt_polygon_geom
  | wkt_polyhedral_surface_geom
  | wkt_triangle_geom
  | wkt_tin_geom
  | wkt_multi_point_geom
  | wkt_multi_line_string_geom
  | wkt_multi_polygon_geom
  | wkt_geometry_collection_geom

wkt_empty_geom ::= EMPTY
wkt_point_geom ::= POINT <<with_dim (
    wkt_empty_geom | wkt_point_text
    )>>
wkt_line_string_geom ::= LINESTRING <<with_dim (
    wkt_empty_geom | wkt_line_string_text
    )>>
wkt_polygon_geom ::= POLYGON <<with_dim (
    wkt_empty_geom | wkt_polygon_text
    )>>
wkt_polyhedral_surface_geom ::= POLYHEDRALSURFACE <<with_dim (
    wkt_empty_geom | <<comma_list_attrs wkt_implicit_polygon_geom>>
    )>>
wkt_triangle_geom ::= TRIANGLE <<with_dim (
    wkt_empty_geom | wkt_polygon_text
    )>>
wkt_tin_geom ::= TIN <<with_dim (
    wkt_empty_geom | <<comma_list_attrs wkt_implicit_polygon_geom>>
    )>>
wkt_multi_point_geom ::= MULTIPOINT <<with_dim (
    wkt_empty_geom | <<comma_list_attrs wkt_implicit_point_geom>>
    )>>
wkt_multi_line_string_geom ::= MULTILINESTRING <<with_dim (
    wkt_empty_geom | wkt_polygon_text
    )>>
wkt_multi_polygon_geom ::= MULTIPOLYGON <<with_dim (
    wkt_empty_geom | <<comma_list_attrs wkt_implicit_polygon_geom>>
    )>>
wkt_geometry_collection_geom ::= GEOMETRYCOLLECTION <<with_dim (
    wkt_empty_geom | <<comma_list_attrs wkt_tagged_geom>>
    )>>

wkt_implicit_point_attr ::= dim_coord {elementType=attributes}
wkt_implicit_no_paren_point_geom ::= wkt_implicit_point_attr {elementType=wkt_point_geom}

private wkt_point_text ::= <<attributes dim_coord>>
wkt_implicit_point_geom ::= wkt_point_text {elementType=wkt_point_geom}
private wkt_line_string_text ::= <<comma_list_attrs wkt_implicit_no_paren_point_geom>>
wkt_implicit_line_string_geom ::= wkt_line_string_text {elementType=wkt_line_string_geom}
private wkt_polygon_text ::= <<comma_list_attrs wkt_implicit_line_string_geom>>
wkt_implicit_polygon_geom ::= wkt_polygon_text {elementType=wkt_polygon_geom}

wkt_wildcard_geom ::= unknown_identifier wkt_wildcard_geom_text {pin=0}
private wkt_wildcard_geom_text ::= <<comma_list_attrs (wildcard_geometry_attr*)>>
wkt_implicit_wildcard_geom ::= wkt_wildcard_geom_text {elementType=wkt_wildcard_geom}
private wildcard_geometry_attr ::= numeric | STRING | wkt_geometry | wkt_implicit_wildcard_geom
